apply plugin: 'com.android.application'

android {
    compileSdkVersion androidSdkVersion
    buildToolsVersion androidToolsVersion

    defaultConfig {
        applicationId basePackageName
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidSdkVersion
        versionCode versionCode
        versionName versionName
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix   '.debug'
        }
    }

    productFlavors.whenObjectAdded { flavor ->

        def flavorData          =   rootProject.ext[flavor.name]

        flavor.applicationId      flavorData.packageName
        flavor.versionCode        flavorData.versionCode
        flavor.versionName        flavorData.versionName
        flavor.buildConfigField   'boolean', fieldShowsAds,    flavorData.showAds
        flavor.resValue           'string',  resAppName,      flavorData.appName
        flavor.resValue           'color',   resAppColorName,  flavorData.appColor

    }


    productFlavors {

        paid {}

        free {}

        promo {}
    }
}

dependencies {

    def presentationDependencies = rootProject.ext.presentationDependencies

    compile  presentationDependencies.androidDesignLib
    compile  presentationDependencies.appCompat
    compile  presentationDependencies.butterKnife
}
